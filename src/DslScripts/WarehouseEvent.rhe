Module Warehouse1
{
    Polymorphic WarehouseEvent
    {
        DateTime EffectiveSince;
        Reference WarehouseItem;
        Reference Warehouse;
        Integer Quantity;
    }

    // Business event of adding items to the warehouse.
    Entity GoodsReceival
    {
        DateTime EffectiveSince { CreationTime; }
        Reference WarehouseItem;
        Reference Warehouse;
        Integer Quantity;

        Is Warehouse1.WarehouseEvent;
    }

    // Business event of removing items from the warehouse.
    Entity GoodsDisptach
    {
        DateTime EffectiveSince { CreationTime; }
        Reference WarehouseItem;
        Reference Warehouse;
        Integer Quantity;

        Is Warehouse1.WarehouseEvent
        {
            Implements Warehouse1.WarehouseEvent.Quantity '-Quantity';
        }
    }

    // Business event of moving items from one warehouse to another.
    Entity GoodsRelocation
    {
        DateTime EffectiveSince { CreationTime; }
        Reference WarehouseItem;
        Reference WarehouseFrom Warehouse1.Warehouse;
        Reference WarehouseTo Warehouse1.Warehouse;
        Integer Quantity;

        Is Warehouse1.WarehouseEvent 'Removing'
        {
            Implements Warehouse1.WarehouseEvent.Warehouse 'WarehouseFromID';
            Implements Warehouse1.WarehouseEvent.Quantity '-Quantity';
        }

        Is Warehouse1.WarehouseEvent 'Adding'
        {
            Implements Warehouse1.WarehouseEvent.Warehouse 'WarehouseToID';
            Implements Warehouse1.WarehouseEvent.Quantity 'Quantity';
        }
    }
}